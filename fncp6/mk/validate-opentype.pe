#!/usr/bin/env fontforge
# ./validate-opentype.pe

has_open_contour     = 0x2
is_intersecting      = 0x4
wrong_direction      = 0x8
glyph_is_flipped     = 0x10
is_missing_extrema   = 0x20
glyph_not_present    = 0x40
points_too_far_apart = 0x40000

i = 1
while(i < $argc)
    Open($argv[i], 1)

    state = Validate()

    if (state != 0)
        Print("ERROR: " + $argv[i] + " (" + state + "):")

        if (state & has_open_contour)
            Print("* Open contour.")
        endif

        if (state & is_intersecting)
            Print("* Intersecting glyph.")
        endif

        if (state & wrong_direction)
            Print("* Contour drawn in the wrong direction.")
        endif

        if (state & glyph_is_flipped)
            Print("* At least one reference in the glyph has been flipped.")
        endif

        if (state & is_missing_extrema)
            Print("* Missing extrema.")
        endif

        if (state & glyph_not_present)
            Print("* A referenced glyph is not present in the font.")
        endif

        if (state & points_too_far_apart)
            Print("* Points too far apart.")
        endif

        Quit(1)
    endif

    Close()

    i++
endloop
